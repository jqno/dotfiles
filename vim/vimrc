" ***************************************
" ***  Define plugins
" ***************************************

packadd! matchit

call plug#begin('~/.vim/plugged')

Plug 'dense-analysis/ale'
Plug 'dhruvasagar/vim-zoom'
Plug 'gcmt/wildfire.vim'
Plug 'janko-m/vim-test'
Plug 'jqno/vim-extract-variable'
Plug 'jqno/vim-textobj-functioncall' | Plug 'kana/vim-textobj-user'
Plug 'jqno/vim-reversal'
Plug 'jqno/fzf.vim' | Plug '/usr/local/opt/fzf' " This is my fork
Plug 'lifepillar/vim-colortemplate'
Plug 'ludovicchabant/vim-gutentags'
Plug 'machakann/vim-swap'
Plug 'plasticboy/vim-markdown'
Plug 'RGBD/vim-xml-indent'
Plug 'rizzatti/dash.vim'
Plug 'scrooloose/nerdtree', { 'on': ['NERDTree', 'NERDTreeToggle', 'NERDTreeFind'] }  " I know, I know. It's just that netrw is incredibly weird and buggy when g:netrw_liststype = 3
Plug 'sheerun/vim-polyglot'
Plug 'SirVer/ultisnips'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-dispatch'
Plug 'tpope/vim-eunuch'
Plug 'tpope/vim-fugitive'
Plug 'vimwiki/vimwiki', { 'on': 'VimwikiIndex' }
Plug 'whiteinge/diffconflicts'
Plug 'wincent/terminus'

call plug#end()



" ***************************************
" ***  Behavior
" ***************************************

" Always use UTF-8
scriptencoding utf-8
" Put swap files and backups in . only as a last resort
set directory-=.
set directory+=.
set backupdir-=.
set backupdir+=.
" Store a big chunk of : command history
set history=1000
" Automatically re-read file in current buffer if it was edited outside vim
set autoread
" Enable filetypes and syntax
filetype plugin indent on
syntax on
" Leaders
let g:mapleader="\<space>"
let g:maplocalleader=','
" Indent automatically
set autoindent
" Backspace over indents, eols, line-starts: just like normal editors
set backspace=indent,eol,start
" Align indents with < and > with indent
set shiftround
" Don't recognise octal numbers with <C-A> and <C-X>
set nrformats-=octal
" Search more easily
set incsearch hlsearch ignorecase smartcase
" Don't wrap lines, but if you do, don't wrap in the middle of a word
set nowrap linebreak
" Join lines more intuitively
set nojoinspaces
set formatoptions+=j
" Managing splits
set splitright
" Smart diffing
set diffopt+=indent-heuristic,algorithm:patience
" Enhanced command-line completion
set wildmenu
" Always ignore node_modules
set wildignore=node_modules
" Completion
set completeopt+=menuone,noinsert
set shortmess+=c


augroup vimStartup
    autocmd!

    " When editing a file, always jump to the last known cursor position.
    " Don't do it when the position is invalid, when inside an event handler
    " (happens when dropping a file on gvim) and for a commit message (it's
    " likely a different one than last time).
    autocmd BufReadPost *
      \ if line("'\"") >= 1 && line("'\"") <= line("$") && &ft !~# 'commit'
      \ |   exe "normal! g`\""
      \ | endif
augroup END



" ***************************************
" ***  Copy/paste
" ***************************************

" Yank everything into the + register so it goes on to the system clipboard
nnoremap Y "+y
vnoremap Y "+y



" ***************************************
" ***  Appearance
" ***************************************

" Make it look purty (but suppress the error if the colorscheme doesn't exist)
set background=dark
set termguicolors
silent! colorscheme reversal
" Highlight merge conflict markers
match ErrorMsg '^\(<\|=\||\|>\)\{7\}\([^=].\+\)\?$'
" Always show the sign column so it doesn't appear and disappear all the time
set signcolumn=yes
" See context while scrolling
set scrolloff=1
set sidescrolloff=5
" Show as much as possible of lines that don't fit in the screen
set display+=lastline
" Show relative line-numbers
set number relativenumber



" ***************************************
" ***  Sessions
" ***************************************

" Don't save mappings and options: vimrc will take care of those
set sessionoptions-=options
" Always overwrite Session.vim in the current directory
command! SaveSession mksession! Session.vim<bar>echo "Saved Session.vim"
" Always load Session.vim from the current directory
command! LoadSession source Session.vim



" ***************************************
" ***  Silly shift-pressing mistakes
" ***************************************

if has('user_commands')
    command! -bang -nargs=* -complete=file E e<bang> <args>
    command! -bang -nargs=* -complete=file W w<bang> <args>
    command! -bang -nargs=* -complete=file Wq wq<bang> <args>
    command! -bang -nargs=* -complete=file WQ wq<bang> <args>
    command! -bang Wa wa<bang>
    command! -bang WA wa<bang>
    command! -bang Q q<bang>
    command! -bang QA qa<bang>
    command! -bang Qa qa<bang>
endif



" ***************************************
" ***  Terminal mode configuration
" ***************************************

" Easier access to Normal mode in Terminal
tnoremap <F1> <C-\><C-N>
" Split navigation
tnoremap <C-H> <C-W>h
tnoremap <C-J> <C-W>j
tnoremap <C-K> <C-W>k
tnoremap <C-L> <C-W>l



" ***************************************
" ***  GVIM configuration
" ***************************************
if has('gui_running')

    " Use a nice font with ligatures
    set guifont=Fira\ Code\ Retina:h13
    if has('gui_macvim')
        set macligatures
    endif

endif



" ***************************************
" ***  VIM itself!
" ***************************************

" Open vimrc
command! EditVimrc e ~/.vim/vimrc
" Reload vimrc
command! ReloadVimrc source ~/.vim/vimrc<bar>doautocmd ColorScheme
" Reload vimrc whenever it's written
augroup AutoSourceVimrc
    autocmd!
    autocmd BufWritePost vimrc ReloadVimrc
augroup END

" Manage a lockfile for Vim plugins
let g:jqno_plugin_lockfile = $HOME . '/.vim/plugin.lock.vim'
function! s:JqnoPlugInitialInstall()
    PlugInstall
    execute 'source ' . g:jqno_plugin_lockfile
    qall
endfunction
command! JqnoPlugInitialInstall call <SID>JqnoPlugInitialInstall()
" Slight hack: this command omits the last letter from the command it replaces, so it doesn't replace the original but does get autocompleted first
command! PlugSnapsho execute 'PlugSnapshot! ' . g:jqno_plugin_lockfile
command! PlugRevert execute 'source ' . g:jqno_plugin_lockfile



" ***************************************
" ***  Plugin configurations
" ***************************************

" *** ALE
" Run linting on save only
let g:ale_lint_on_text_changed = 'never'

" *** FZF
let g:fzf_command_prefix = 'Fzf'
let g:fzf_preview_window = {'options': '--delimiter : --nth 4..'}
" Key mappings within FZF to define how to open a file
" Ctrl+Q sends results to quickfix
let g:fzf_action = {
    \ 'enter': 'e',
    \ 'space': 'split',
    \ 'ctrl-q': 'sink'
    \ }
" Define colors
let g:fzf_colors = {
    \ 'fg':      ['fg', 'Normal'],
    \ 'bg':      ['bg', 'Normal'],
    \ 'hl':      ['fg', 'String'],
    \ 'fg+':     ['fg', 'CursorLine'],
    \ 'bg+':     ['bg', 'CursorLine'],
    \ 'hl+':     ['fg', 'String'],
    \ 'info':    ['fg', 'PreProc'],
    \ 'border':  ['fg', 'Ignore'],
    \ 'prompt':  ['fg', 'Conditional'],
    \ 'pointer': ['fg', 'Exception'],
    \ 'marker':  ['fg', 'Keyword'],
    \ 'spinner': ['fg', 'Label'],
    \ 'header':  ['fg', 'Comment']
    \ }
" Adds preview window to FzfAg
command! -bang -nargs=+ FzfAg
    \ call fzf#vim#ag_raw(<q-args>, fzf#vim#with_preview(g:fzf_preview_window), <bang>0)

" *** Hexokinase
let g:Hexokinase_ftAutoload = ['css', 'scss', 'html']
let g:Hexokinase_refreshEvents = ['BufWritePost']

" *** Terminus
" Having this enabled causes garbage characters to be printedin :terminal when switching macOS spaces
let g:TerminusFocusReporting=0

" *** UltiSnips
let g:UltiSnipsExpandTrigger = '<S-Tab>'
let g:UltiSnipsJumpForwardTrigger = '<NOP>'

" *** Vim-Markdown
let g:vim_markdown_frontmatter = 1
let g:vim_markdown_folding_disabled = 1

" *** Vim-swap
let g:swap_no_default_key_mappings = 1

" *** Vim-Test
let test#strategy = 'dispatch'

" *** VimWiki
let g:vimwiki_location = '~/Dropbox/vimwiki'
let g:vimwiki_list = [{'path': g:vimwiki_location, 'syntax': 'markdown', 'ext': '.md'}]
let g:vimwiki_global_ext = 0
let g:vimwiki_folding = 'expr'
" Search VimWiki using Fzf and Ag
command! -nargs=+ FzfVimwiki 
    \ call fzf#vim#ag_raw(
    \   <q-args> . ' ' . g:vimwiki_location,
    \   fzf#vim#with_preview(g:fzf_preview_window))

" *** Vim-Zoom
let g:zoom#statustext = '[ZOOMED]'

" *** Wildfire
let g:wildfire_objects = ["i'", 'i"', 'i)', 'i]', 'i}', 'if', 'ip', 'it']



" ***************************************
" ***  Mappings
" ***************************************

" *** No prefix: general mappings

" Pressing up in a long line gets you to the above line 'in the screen',
" but if you precede it with a count, you get the old behavior
" and if the count > 5, we'll also add it to the jump list so we can do C-O and C-I
nnoremap <expr> j v:count ? (v:count > 5 ? "m'" . v:count : '') . 'j' : 'gj'
nnoremap <expr> k v:count ? (v:count > 5 ? "m'" . v:count : '') . 'k' : 'gk'
vnoremap <expr> j v:count ? (v:count > 5 ? "m'" . v:count : '') . 'j' : 'gj'
vnoremap <expr> k v:count ? (v:count > 5 ? "m'" . v:count : '') . 'k' : 'gk'

" Keep the visual selection after < and > so we can repeat these buttons
" instead of tring to find .
vnoremap < <gv
vnoremap > >gv
" Allow for easy window switching
nnoremap <C-H> <C-W>h
nnoremap <C-J> <C-W>j
nnoremap <C-K> <C-W>k
nnoremap <C-L> <C-W>l
" Allow for easy window resizing
" (˙∆˚¬ are Alt+hjkl on macOS)
noremap <silent> ˙ :call ResizeLikeTmux('h', 1)<CR>
noremap <silent> ∆ :call ResizeLikeTmux('j', 1)<CR>
noremap <silent> ˚ :call ResizeLikeTmux('k', 1)<CR>
noremap <silent> ¬ :call ResizeLikeTmux('l', 1)<CR>
" Mappings for vim-commentary
xmap \ <Plug>Commentary
nmap \ <Plug>Commentary
omap \ <Plug>Commentary
nmap \\ <Plug>CommentaryLine
nmap <leader>\ <Plug>Commentary<Plug>Commentary
" Mappings from Unimpaired that I actually use
map <silent> [b :bprevious<CR>
map <silent> ]b :bnext<CR>
map <silent> [q :cprevious<CR>
map <silent> ]q :cnext<CR>
map <silent> [Q :cfirst<CR>
map <silent> ]Q :clast<CR>
" Handy ALE mappings in Unimpaired style
map <silent> [W <Plug>(ale_first)
map <silent> [w <Plug>(ale_previous)
map <silent> ]w <Plug>(ale_next)
map <silent> ]W <Plug>(ale_last)
" Center on the line with the match when searching
nnoremap n nzz
nnoremap N Nzz
" When finding `=` include the whitespace around it
nnoremap t= /\s*=<CR>:nohls<CR>
nnoremap T= ?=\s*?e<CR>:nohls<CR>
" ToggleMovement : 0 moves to beginning of text first, then to beginning of line
nnoremap <silent> 0 :call ToggleMovement('^', '0')<CR>
" Open VimWiki
nnoremap <F12> :VimwikiIndex<CR>

" Close extraneous windows
nnoremap <leader><Esc> :pclose<CR>:cclose<CR>:lclose<CR>

" *** Prefix <leader><leader>: change settings
nnoremap <silent> <leader><leader>2 :call SetTabstop(2)<CR>:echo "Indentation level: 2"<CR>
nnoremap <silent> <leader><leader>4 :call SetTabstop(4)<CR>:echo "Indentation level: 4"<CR>
nnoremap <silent> <leader><leader>8 :call SetTabstop(8)<CR>:echo "Indentation level: 8"<CR>
nnoremap <silent> <leader><leader>0 :set noexpandtab nosmarttab tabstop=4 shiftwidth=4 softtabstop=4<CR>:echo "Indentation level: tab"<CR>
nnoremap <silent> <leader><leader>b :Gblame<CR>
nnoremap <silent> <leader><leader>t :call ToggleListChars()<CR>
nnoremap <silent> <leader><leader>w :call ToggleWrap()<CR>
nnoremap <silent> <leader><leader>/ :nohlsearch<CR>
nnoremap <silent> <leader><leader><leader> za

" *** Prefix <leader>f: finding things
nnoremap <leader>fb :FzfBuffers<CR>
nnoremap <leader>ff :FzfFiles<CR>
nnoremap <leader>fg :FzfAg<Space>
nnoremap <leader>fh :FzfHelptags<CR>
nnoremap <leader>ft :FzfTags<CR>
nnoremap <leader>fo :FzfBTags<CR>
nnoremap <leader>f* "ayiw:FzfAg <C-R>a<CR>
nnoremap <leader>f] :sp<CR>:exec("tag " . expand("<cword>"))<CR>
nnoremap <leader>fd :Dash<CR>
nnoremap <leader>fn :NERDTreeToggle<CR>
nnoremap <leader>fN :NERDTreeFind<CR>
nnoremap <leader>f<F12> :FzfVimwiki<Space>

" " *** Prefix <leader>g: going to places
" nmap <silent> <leader>gd <Plug>(coc-definition)
" nmap <silent> <leader>gt <Plug>(coc-type-definition)
" nmap <silent> <leader>gi <Plug>(coc-implementation)
" nmap <silent> <leader>gr <Plug>(coc-references)

" " *** Prefix <leader>m: running make
" nnoremap <leader>mm :Make<CR>
" nmap <silent> <leader>mf <Plug>(coc-format)
" vmap <silent> <leader>mf <Plug>(coc-format-selected)

" *** Prefix <leader>t: running tests
nnoremap <leader>ta :TestSuite<CR>
nnoremap <leader>tf :TestFile<CR>
nnoremap <leader>tt :TestNearest<CR>

" *** Prefix <leader>r: refactorings
nmap <leader>rv <Plug>(extractVariableNormal)
vmap <leader>rv <Plug>(extractVariableVisual)
nnoremap <Leader>r* /\<<C-R>=expand('<cword>')<CR>\>\C<CR>``cgn
nnoremap <Leader>r# ?\<<C-R>=expand('<cword>')<CR>\>\C<CR>``cgN
nmap <leader>r< <Plug>(swap-prev)
nmap <leader>r> <Plug>(swap-next)
" nmap <leader>rf <Plug>(coc-fix-current)
" nnoremap <leader>ro :call CocAction('runCommand', 'editor.action.organizeImport')<CR>
" nmap <leader>rr <Plug>(coc-rename)
" nmap <leader>r<cr> <Plug>(coc-codeaction)
" vmap <leader>r<cr> <Plug>(coc-codeaction-selected)
" nmap <leader>r<space> <Plug>(coc-refactor)

" *** Prefix <leader>b: buffers
nnoremap <silent> <leader>bb :b#<CR>
nnoremap <silent> <leader>bd :bp\|bd #<CR>

" *** Prefix <leader>w: windows
nnoremap <leader>w_ <C-W>_
nnoremap <leader>w= <C-W>=
nmap <leader>w<space> <Plug>(zoom-toggle)

" *** Command-line mode
" Expand %% to the directory of the currently open file, in Command-Line mode.
" # is the 'other' file, because % would be the Command-Line window itself.
cnoremap %% <C-R>=expand('#:h').'/'<CR>

